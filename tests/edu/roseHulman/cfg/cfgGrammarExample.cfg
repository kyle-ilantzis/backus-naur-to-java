/*--------------------------------- 
Below is the grammar for CFGs 
written using the grammar for CFGs. 
Four exceptions must be made when 
encoding the grammar in the grammar:
1. GOES_TO stands for the 
   relation '::='
2. NEWLINE stands for 1 or more newlines, 
   possibly separated by other whitespace
3. EPSILON stands for the symbol 
   'e', which represents the empty 
   string
4. OR stands for the symbol '|', 
   which represents alternation
The lexical syntax admits C-style comments
like this one.
-----------------------------------*/
<Grammar>	::= <ProdList>

<ProdList>	::= <ProdBlock> <ProdList>
	| e

<ProdBlock>	::= nt GOES_TO <RightSide>

<RightSide>	::= <Symbol> <SymbolList> NEWLINE <OtherProds>
	| EPSILON NEWLINE <OtherProds>

<Symbol>	::= nt
	| t

<SymbolList>	::= <Symbol> <SymbolList>
	| e

<OtherProds>	::= OR <RightSide>
	| e
